apply plugin: 'com.android.application'

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

def getPropInt(def key) {
    return Integer.valueOf((String) getProperty(key))
}

def getPropString(def key) {
    return (String) getProperty(key)
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    signingConfigs {
        release {
            storeFile file(getPropString("STORE_FILE"))
            storePassword getPropString("STORE_PASSWORD")
            keyAlias getPropString("KEY_ALIAS")
            keyPassword getPropString("KEY_PASSWORD")
        }
        debug {
            storeFile file(getPropString("STORE_FILE"))
            storePassword getPropString("STORE_PASSWORD")
            keyAlias getPropString("KEY_ALIAS")
            keyPassword getPropString("KEY_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "com.hzy.base"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner rootProject.ext.dependencies["androidJUnitRunner"]
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release//设置签名信息
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            debuggable true
            jniDebuggable true
        }
    }
}

/**
 * 设置打包apk带版本号
 */
android.applicationVariants.all { variant ->
    variant.outputs.all {
        //这里修改apk文件名
        outputFileName = "Base-${variant.name}-v${variant.versionName}.apk"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    implementation rootProject.ext.dependencies["cardview"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    implementation project(':utils')
    implementation project(':views')
}
